"""
Build script for bun-webkit


## Build
```
bazel build //:bun-webkit --platforms=//:linux_x64 -c opt
```

"""

load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")
load("@bazel_skylib//lib:selects.bzl", "selects")

filegroup(
    name = "srcs",
    srcs = glob(["**"]),
    visibility = ["//visibility:private"],
)

cmake(
    name = "bun-webkit",
    cache_entries = {
                        "PORT": "JSCOnly",  # Port
                    } |
                    # Platform
                    select(
                        {
                            ":macOS": {
                                "ENABLE_STATIC_JSC": "ON",
                                "ENABLE_SINGLE_THREADED_VM_ENTRY_SCOPE": "ON",
                                "ENABLE_BUN_SKIP_FAILING_ASSERTIONS": "ON",
                                "USE_THIN_ARCHIVES": "OFF",
                                "ENABLE_FTL_JIT": "ON",
                                "USE_BUN_JSC_ADDITIONS": "ON",
                                "CMAKE_EXE_LINKER_FLAGS": "-fuse-ld=lld",
                                "CMAKE_AR": "$(which llvm-ar)",
                                "CMAKE_RANLIB": "$(which llvm-ranlib)",
                                "ALLOW_LINE_AND_COLUMN_NUMBER_IN_BUILTINS": "ON",
                                "CMAKE_OSX_DEPLOYMENT_TARGET": "11.0",
                                "PTHREAD_JIT_PERMISSIONS_API": "1",
                                "USE_PTHREAD_JIT_PERMISSIONS": "ON",
                                "ENABLE_REMOTE_INSPECTOR": "ON",
                                "CMAKE_EXPORT_COMPILE_COMMANDS": "ON",
                            },
                            ":Linux": {
                                "ENABLE_STATIC_JSC": "ON",
                                "ENABLE_BUN_SKIP_FAILING_ASSERTIONS": "ON",
                                "USE_THIN_ARCHIVES": "OFF",
                                "USE_BUN_JSC_ADDITIONS": "ON",
                                "ENABLE_FTL_JIT": "ON",
                                "CMAKE_EXPORT_COMPILE_COMMANDS": "ON",
                                "ALLOW_LINE_AND_COLUMN_NUMBER_IN_BUILTINS": "ON",
                                "ENABLE_SINGLE_THREADED_VM_ENTRY_SCOPE": "ON",
                            },
                            ":Windows": {},
                        },
                        no_match_error = "Please provide platform",
                    ) |
                    # Architecture
                    select(
                        {
                            ":mac_and_x64": {
                                "CMAKE_OSX_ARCHITECTURES": "x86_64",
                            },
                            ":mac_and_arm64": {
                                "CMAKE_OSX_ARCHITECTURES": "arm64",
                            },
                            ":linux_and_x86": {
                                "CMAKE_CXX_FLAGS": "-m32",
                                "CMAKE_C_FLAGS": "-m32",
                                "CMAKE_EXE_LINKER_FLAGS": "-m32",
                                "CMAKE_SHARED_LINKER_FLAGS": "-m32",
                            },
                            ":linux_and_x64": {
                                "CMAKE_CXX_FLAGS": "-m64",
                                "CMAKE_C_FLAGS": "-m64",
                                "CMAKE_EXE_LINKER_FLAGS": "-m64",
                                "CMAKE_SHARED_LINKER_FLAGS": "-m64",
                            },
                            ":windows_and_x86": {
                                "CMAKE_CXX_FLAGS": "-m32",
                                "CMAKE_C_FLAGS": "-m32",
                                "CMAKE_EXE_LINKER_FLAGS": "-m32",
                                "CMAKE_SHARED_LINKER_FLAGS": "-m32",
                            },
                            ":windows_and_x64": {
                                "CMAKE_CXX_FLAGS": "-m64",
                                "CMAKE_C_FLAGS": "-m64",
                                "CMAKE_EXE_LINKER_FLAGS": "-m64",
                                "CMAKE_SHARED_LINKER_FLAGS": "-m64",
                            },
                        },
                        no_match_error = "Please provide platform",
                    ),
    generate_args = [
        "-GNinja",
    ],
    lib_source = ":srcs",
    tags = ["no-sandbox"],  # ccache need write access to cache dir
    # out_static_libs = ["libbun-webkit"],
)

config_setting(
    name = "os",
    constraint_values = [
        ":macOS",
        ":Windows",
        ":Linux",
    ],
)

config_setting(
    name = "arch",
    constraint_values = [
        ":x86",
        ":x64",
        ":arm64",
    ],
)

# constraint_setting acts as an enum type, and constraint_value as an enum value.
constraint_setting(name = "platform")

constraint_value(
    name = "macOS",
    constraint_setting = "platform",
)

constraint_value(
    name = "Windows",
    constraint_setting = "platform",
)

constraint_value(
    name = "Linux",
    constraint_setting = "platform",
)

constraint_setting(name = "architecture")

constraint_value(
    name = "x86",
    constraint_setting = "architecture",
)

constraint_value(
    name = "x64",
    constraint_setting = "architecture",
)

constraint_value(
    name = "arm64",
    constraint_setting = "architecture",
)

# for condition AND of select
selects.config_setting_group(
    name = "windows_and_x86",
    match_all = [
        "//:Windows",
        "//:x86",
    ],
)
selects.config_setting_group(
    name = "windows_and_x64",
    match_all = [
        "//:Windows",
        "//:x64",
    ],
)

selects.config_setting_group(
    name = "mac_and_x64",
    match_all = [
        "//:macOS",
        "//:x64",
    ],
)
selects.config_setting_group(
    name = "mac_and_arm64",
    match_all = [
        "//:macOS",
        "//:arm64",
    ],
)

selects.config_setting_group(
    name = "linux_and_x86",
    match_all = [
        "//:Linux",
        "//:x86",
    ],
)
selects.config_setting_group(
    name = "linux_and_x64",
    match_all = [
        "//:Linux",
        "//:x64",
    ],
)


# Windows
platform(
    name = "win32",
    constraint_values = [
        ":Windows",
        ":x86",
    ],
)

platform(
    name = "win64",
    constraint_values = [
        ":Windows",
        ":x64",
    ],
)

# macOS
platform(
    name = "mac_intel",
    constraint_values = [
        ":macOS",
        ":x64",
    ],
)

platform(
    name = "mac_apple_silicon",
    constraint_values = [
        ":macOS",
        ":arm64",
    ],
)

# Linux
platform(
    name = "linux_x86",
    constraint_values = [
        ":Linux",
        ":x86",
    ],
)

platform(
    name = "linux_x64",
    constraint_values = [
        ":Linux",
        ":x64",
    ],
)
